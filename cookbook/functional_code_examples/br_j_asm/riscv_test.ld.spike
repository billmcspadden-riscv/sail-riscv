OUTPUT_ARCH( "riscv" )
ENTRY(_start)

/*******************************************************************************************
///             LICENSE:
///
///              Copyright 2021 Seagate Technology LLC and/or its Affiliates
///
///              Licensed under the Apache License, Version 2.0 (the "License");
///              you may not use this file except in compliance with the License.
///              You may obtain a copy of the License at
///
///                  http://www.apache.org/licenses/LICENSE-2.0
///
///              Unless required by applicable law or agreed to in writing, software
///              distributed under the License is distributed on an "AS IS" BASIS,
///              WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
///              See the License for the specific language governing permissions and
///              limitations under the License.
********************************************************************************************/

/*----------------------------------------------------------------------*/
/* Sections                                                             */
/*----------------------------------------------------------------------*/

SECTIONS
{

  /* text: test code section */
  . = 0x80000040;
  .text.init : { *(.text.init) }

  .text : { *(.text) }

  /* data segment */
  . = 0x80080000;
  .tohost ALIGN(0x1000) : { *(.tohost) }
  .data : { *(.data) }
  .rodata : { *(.rodata) *(.rodata.*) *(.gnu.linkonce.r.*) }
  .sdata : {
    __global_pointer$ = . + 0x800;
    *(.srodata.cst16) *(.srodata.cst8) *(.srodata.cst4) *(.srodata.cst2) *(.srodata*)
    *(.sdata .sdata.* .gnu.linkonce.s.*)
  }

  /* bss segment */
  .sbss : {
    *(.sbss .sbss.* .gnu.linkonce.sb.*)
    *(.scommon)
  }
  .bss : { *(.bss) }

  /* thread-local data segment */
  .tdata :
  {
    _tls_data = .;
    *(.tdata.begin)
    *(.tdata)
    *(.tdata.end)
  }
  .tbss :
  {
    *(.tbss)
    *(.tbss.end)
  }

  __malloc_start = .;
  . = . + 32768;

  /* End of uninitalized data segement */
  _end = .;

}
